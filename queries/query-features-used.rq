PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX q: <https://text2sparql.aksw.org/2025/corporate/queries/>
PREFIX shui: <https://vocab.eccenca.com/shui/>

SELECT ?table_line
FROM <https://text2sparql.aksw.org/2025/corporate/>
WHERE
{
  {
    SELECT DISTINCT 
      ?Feature 
      (COUNT(?query) AS ?Queries) 
      (ROUND(?Queries / ?nrOfQry * 100 * 10) / 10 AS ?Percent) 
    WHERE {
      {
        SELECT (0 + COUNT(?q) AS ?nrOfQry) WHERE {
          ?q a shui:SparqlQuery .
        }
      }
      ?query a shui:SparqlQuery .
      ?query q:feature ?featureR.
      BIND (REPLACE(STR(?featureR), "https://text2sparql.aksw.org/2025/corporate/queries/features/", "") AS ?Feature)
    } 
    GROUP BY ?Feature ?nrOfQry
    ORDER BY DESC(?Queries)
  }
  BIND (CONCAT(STR(?Feature), " & ", STR(?Queries), " & ", STR(?Percent), " \\% \\\\") AS ?table_line)
}

